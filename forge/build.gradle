plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'dev.architectury.loom'
}

base {
    archivesName = "${mod_name}-forge-${minecraft_version}"
}

loom {
    forge {
        mixinConfig "${mod_id}.mixins.json"
        mixinConfig "${mod_id}.forge.mixins.json"

        convertAccessWideners = true
    }

    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }

    runs {
        client {
            client()
            setConfigName("Forge Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Forge Server")
            ideConfigGenerated(true)
            runDir("run")
        }
        datagen {
            data()
            setConfigName("Forge Datagen")
            programArgs("--mod", "${mod_id}", "--all", "--output", file("src/generated/resources").absolutePath)
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

sourceSets.main.resources.srcDir 'src/generated/resources'

repositories {
    maven {
        name = "Sinytra"
        url = "https://maven.su5ed.dev/releases"
    }
    mavenLocal()
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    forge "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    mappings loom.officialMojangMappings()

    modImplementation "dev.su5ed.sinytra.fabric-api:fabric-api:${forge_fabric_version}"

    compileOnly project(":common")
}

tasks.withType(JavaCompile).configureEach {
    source(project(":common").sourceSets.main.allSource)
}
tasks.withType(Javadoc).configureEach {
    source(project(":common").sourceSets.main.allJava)
}
tasks.named("sourcesJar", Jar) {
    from(project(":common").sourceSets.main.allSource)
}

processResources {
    from project(":common").sourceSets.main.resources
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
